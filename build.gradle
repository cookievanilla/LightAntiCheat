plugins {
    id 'com.gradleup.shadow' version '9.3.1'
    id 'java'
    id 'maven-publish'
}

group = 'me.vekster'
version = '1.2.7'

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.opencollab.dev/main/' }
    maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url 'https://repo.tcoded.com/releases' }
}

def minecraftApiVersion = '1.21.11-R0.1-SNAPSHOT'

dependencies {
    implementation 'org.jetbrains:annotations:26.0.2-1'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    compileOnly "io.papermc.paper:paper-api:${minecraftApiVersion}"
    compileOnly 'io.netty:netty-all:4.1.131.Final'
    implementation files('impl/LightInjector-1.0.2-forked.jar')
    implementation 'com.tcoded:FoliaLib:0.5.1'
    compileOnly 'org.geysermc.floodgate:api:2.2.4-SNAPSHOT'
    implementation 'com.tchristofferson:ConfigUpdater:2.2'
    implementation fileTree('impl/multiversion')
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/tiredvekster/LightAntiCheat"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(8)
}

tasks.register('verifyModernBukkitSymbols') {
    doLast {
        def forbidden = [
                'Material.GRASS',
                'PotionEffectType.FAST_DIGGING',
                'PotionEffectType.JUMP',
                'EntityType.BOAT',
                'EntityType.PRIMED_TNT',
                'Enchantment.ARROW_KNOCKBACK',
                'Enchantment.LUCK'
        ]

        def violations = []
        fileTree('src/main/java').matching { include '**/*.java' }.files.each { file ->
            def text = file.getText('UTF-8')
            forbidden.each { symbol ->
                def regex = java.util.regex.Pattern.quote(symbol)
                if (text =~ /\b${regex}\b/) {
                    violations << "${file}: ${symbol}"
                }
            }
        }

        if (!violations.isEmpty()) {
            throw new GradleException('Found removed Bukkit API symbols:\n' + violations.join('\n'))
        }
    }
}

tasks.named('compileJava') {
    dependsOn(tasks.named('verifyModernBukkitSymbols'))
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml']) {
        expand props
    }
}

shadowJar {
    exclude('LICENSE', 'META-INF/**')
    relocate('com.fren_gor', 'org.fren_gor')
    relocate('com.tchristofferson', 'org.tchristofferson')
    relocate('com.tcoded', 'org.tcoded')
    archiveClassifier.set('')
    destinationDirectory.set(file(destinationPath))
}
